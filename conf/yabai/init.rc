#!/usr/bin/env zsh

typeset -g _yabaiconf_basedir="${0:h:A}"

typeset -ga _yabairc_manage_list=()
typeset -ga _yabairc_ignore_list=()
typeset -ga _yabairc_config_list=()

function _nospace() {
  echo -n ${@//[[:blank:]]/_}
}

function _yabairc_int_ignore() {
  yabai -m rule --add label="manage-ignore-$(_nospace $@)" title="^$2$" app="^$1$" manage=off
  echo "Ignoring '$1' with title '$2'"
}

function _yabairc_int_manage() {
  yabai -m rule --add label="manage-include-$(_nospace $@)" app="^$@$" manage=on
  echo "Managing '$@'"
}

function _yabairc_int_config() {
  yabai -m config $1 $2
  echo "Set global '$1' to '$2'"
}

function ignore() {
  local a="$1"
  shift 1
  local b="$@"
  _yabairc_ignore_list+=("$a" "$b")
}

function manage() {
  local a="$@"
  _yabairc_manage_list+=("$a")
}

function config() {
  local a="$1"
  shift 1
  local b="$@"
  _yabairc_config_list+=("$a" "$b")
}

function commit() {
  yabai -m rule --add label='manage-ignore-all' app='^.*$' manage=off

  sleep 0.1

  local a='' b=''

  for a b in "${_yabairc_config_list[@]}"; do
    _yabairc_int_config "$a" "$b"
  done

  sleep 0.1

  for a in "${_yabairc_manage_list[@]}"; do
    _yabairc_int_manage "$a"
  done

  sleep 0.1

  for a b in "${_yabairc_ignore_list[@]}"; do
    _yabairc_int_ignore "$a" "$b"
  done

  sleep 0.1
}
