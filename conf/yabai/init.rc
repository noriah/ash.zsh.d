#!/usr/bin/env zsh

typeset -g _yabaiconf_basedir="${0:h:A}"

typeset -ga _yabairc_manage_list=()
typeset -ga _yabairc_ignore_list=()
typeset -ga _yabairc_config_list=()

function _nospace() {
  echo -n ${@//[[:blank:]]/_}
}

function _yabairc_int_ignore() {
  local title=''
  if [ ! -z "$2" ]; then
    title=" title$4=\"$2\"" 
  fi
  yabai -m rule --add label="manage-ignore-$(_nospace $@)"$title app$3="$1" manage=off 
  echo "Ignoring $3'$1' $title"
}

function _yabairc_int_manage() {
  local title=''
  if [ ! -z "$2" ]; then
    title=" title$4=\"$2\"" 
  fi
  yabai -m rule --add label="manage-include-$(_nospace $@)"$title app$3="$1" manage=on 
  echo "Managing $3'$1' $title"
}

function _yabairc_int_config() {
  yabai -m config $1 $2
  echo "Set global '$1' to '$2'"
}

function ignore() {
  _yabairc_ignore_list+=("$1" "$2" "$3" "$4")
}

function manage() {
  _yabairc_manage_list+=("$1" "$2" "$3" "$4")
}

function config() {
  _yabairc_config_list+=("$1" "$2")
}

function commit() {
  #yabai -m rule --add label='manage-ignore-all' app='^.*$' manage=off

  sleep 0.1

  local a='' b='' c='' d=''

  for a b c d in "${_yabairc_manage_list[@]}"; do
    _yabairc_int_manage "$a" "$b" "$c" "$d"
  done

  _yabairc_manage_list=()

  sleep 0.1

  for a b c d in "${_yabairc_ignore_list[@]}"; do
    _yabairc_int_ignore "$a" "$b" "$c" "$d"
  done

  _yabairc_ignore_list=()

  sleep 0.1

  for a b in "${_yabairc_config_list[@]}"; do
    _yabairc_int_config "$a" "$b"
  done

  _yabairc_config_list=()

  sleep 0.1
}
