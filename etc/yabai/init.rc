#!/usr/bin/env zsh

typeset -g _yabaiconf_basedir="${0:h:A}"

typeset -ga _yabairc_manage_list=()
typeset -ga _yabairc_ignore_list=()
typeset -ga _yabairc_rule_list=()
typeset -ga _yabairc_config_list=()

function _nospace() {
  echo -n ${@//[[:blank:]]/_}
}



function _yabairc_int_rule() {
  local label="rule-$(_nospace $@)"
  yabai -m rule --add label="$label" ${@}
  echo "Rule $@"
}

function _yabairc_int_ignore() {
  local title=''
  if [ ! -z "$2" ]; then
    title="title$4=$2"
  fi
  yabai -m rule --add label="manage-ignore-$(_nospace $@)" $title app$3="$1" manage=off
  echo "Ignoring $3'$1' $title"
}

function _yabairc_int_manage() {
  function join_by {
    local IFS="|"
    echo "$*"
  }

  local keys=$(join_by ${_yabairc_manage_list[@]})
  _yabairc_int_ignore "^($keys)$" '' '!'
}

function _yabairc_int_config() {
  yabai -m config $1 $2
  echo "Set global '$1' to '$2'"
}

function rule() {
  _yabairc_rule_list+=( "$1" "$2" "$3" "$4" "$5" "$6" )
}

function ignore() {
  _yabairc_ignore_list+=("$1" "$2" "$3" "$4")
}

function manage() {
  _yabairc_manage_list+=("$@")
}

function config() {
  _yabairc_config_list+=("$1" "$2")
}

function commit() {
  sleep 0.1

  local a='' b='' c='' d='' e='' f=''

  if [ ! -z "$_yabairc_manage_list" ]; then
    _yabairc_int_manage
  fi

  _yabairc_manage_list=()

  sleep 0.1

  for a b c d in "${_yabairc_ignore_list[@]}"; do
    _yabairc_int_ignore "$a" "$b" "$c" "$d"
  done

  _yabairc_ignore_list=()

  sleep 0.1

  for a b in "${_yabairc_config_list[@]}"; do
    _yabairc_int_config "$a" "$b"
  done

  _yabairc_config_list=()

  sleep 0.1


  for a b c d e f in "${_yabairc_rule_list[@]}"; do
    _yabairc_int_rule "$a" "$b" "$c" "$d" "$e" "$f"
  done

  _yabairc_rule_list=()

  sleep 0.1

  echo "Commit complete"
}
